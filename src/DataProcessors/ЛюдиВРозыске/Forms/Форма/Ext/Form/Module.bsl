
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказатьСледующегоВРозыске();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СледующийВРозыске(Команда)
	
	Найден = Ложь;
	ПоказатьСледующегоВРозыске(Найден);
	
	Если Найден Тогда
		ПоказатьПредупреждение(, "ru = 'Истина ложна. Я лгун.'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция Пол(ПолСтрокой)

	Значения = Новый Соответствие;
	Значения.Вставить("male", Перечисления.ВидыПолов.Муж);
	Значения.Вставить("female", Перечисления.ВидыПолов.Жен);
	
	Пол = Значения.Получить(ПолСтрокой);
	
	Если Пол = Неопределено Тогда
		Пол = Перечисления.ВидыПолов.ХЗ;
	КонецЕсли;
	
	Возврат Пол;

КонецФункции

&НаСервере
Процедура ПоказатьСледующегоВРозыске(Найден = Ложь)
	
	Ответ = КоннекторHTTP.GetJson(URL());
	
	Результаты = Ответ.Получить("results");
	
	Если ТипЗнч(Результаты) = Тип("Массив") Тогда
	
		Для каждого Результат Из Результаты Цикл
			
			Если НЕ ТипЗнч(Результат) = Тип("Соответствие") Тогда
				Продолжить;
			КонецЕсли;

			Объект.Погоняло = СвойствоСоответствияРекурсивно(Результат, "name.last");
			Объект.АдресПоследнегоОбнаружения = СвойствоСоответствияРекурсивно(Результат, "location.state");
			Объект.ФотоМордыЛица = СвойствоСоответствияРекурсивно(Результат, "picture.large");
			Объект.Идентификатор = СвойствоСоответствияРекурсивно(Результат, "login.uuid");

			ДатаРожденияСтрокой = СвойствоСоответствияРекурсивно(Результат, "dob.date");
			Объект.ДатаРождения = ПрочитатьДатуJSON(ДатаРожденияСтрокой, ФорматДатыJSON.ISO);

			Объект.Возраст = СвойствоСоответствияРекурсивно(Результат, "dob.age");
			
			ПолСтрокой = СвойствоСоответствияРекурсивно(Результат, "gender");
			Объект.Пол = Пол(ПолСтрокой);
			
			Найден = ЭтоИскомыйПерсонаж();

		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЭтоИскомыйПерсонаж()

	Возврат Объект.Погоняло = "М." И Объект.АдресПоследнегоОбнаружения = "Воронеж" И Объект.Возраст > 18;

КонецФункции

// Возвращает значение свойства структуры.
//
// Параметры:
//   Структура - Структура, ФиксированнаяСтруктура - Объект, из которого необходимо прочитать значение ключа.
//   КлючиСтрокой - Строка - Имена свойств структур через точку.
//   ЗначениеПоУмолчанию - Произвольный - Необязательный. Возвращается когда в структуре нет значения по указанному
//                                        ключу.
//       Для скорости рекомендуется передавать только быстро вычисляемые значения (например примитивные типы),
//       а инициализацию более тяжелых значений выполнять после проверки полученного значения (только если это
//       требуется).
//
// Возвращаемое значение:
//   Произвольный - Значение свойства структуры. ЗначениеПоУмолчанию если в структуре нет указанного свойства.
//
&НаСервереБезКонтекста
Функция СвойствоСоответствияРекурсивно(Соответствие, Знач КлючиСтрокой, ЗначениеПоУмолчанию = Неопределено)
	
	Если Не ТипЗнч(Соответствие) = Тип("Соответствие") Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Разделитель = ".";
	
	Ключи = СтрРазделить(КлючиСтрокой, Разделитель);
	
	Если Ключи.Количество() = 0 Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли; 
	
	Ключ = Ключи[0];
	
	Значение = СвойствоСоответствия(Соответствие, Ключ, ЗначениеПоУмолчанию);
	
	Если Ключи.Количество() > 1 Тогда
		// Последний уровень
		Ключи.Удалить(0);
		КлючиСтрокой = СтрСоединить(Ключи, Разделитель);
		
		Если ТипЗнч(Значение) = Тип("Соответствие") Тогда
			
			Значение = СвойствоСоответствияРекурсивно(Значение, КлючиСтрокой, ЗначениеПоУмолчанию);
			
		Иначе	
			
			Значение = ЗначениеПоУмолчанию;
			
		КонецЕсли; 
		
	КонецЕсли; 
	
	Возврат Значение;
	
КонецФункции

&НаСервереБезКонтекста
Функция СвойствоСоответствия(Соответствие, Ключ, ЗначениеПоУмолчанию = Неопределено)
	
	Если Соответствие = Неопределено Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Результат = Соответствие.Получить(Ключ);
	Если Результат = Неопределено Тогда
		Результат = ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция URL()
	Возврат "https://randomuser.me/api/";
КонецФункции;

#КонецОбласти

