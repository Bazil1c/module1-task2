
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПолучитьИЗаполнитьРейтинговуюТаблицу();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СортироватьПоВыполненнымЗаданиям(Команда)
	
	РейтинговаяТаблица.Сортировать("ВыполненныхЗаданий УБЫВ");
	
КонецПроцедуры

&НаКлиенте
Процедура СортироватьПоСпасеннымЗаложникам(Команда)
	
	РейтинговаяТаблица.Сортировать("СпасенныхЗаложников УБЫВ");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняем GET запрос к сервису рейтинговых таблиц и получаем показатели наемников.
//
&НаСервере
Процедура ПолучитьИЗаполнитьРейтинговуюТаблицу()
	
	Люди = ПолучитьДанныеССервера();
	
	Для Каждого Человек Из Люди Цикл
		
		Имя = Человек["name"];
		
		Если ЭтоИсключаемыйНаемник(Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = РейтинговаяТаблица.Добавить();
		НоваяСтрока.Наемник = Имя;
		
		НоваяСтрока.ВыполненныхЗаданий = СтрЗаменить(Человек["address"]["geo"]["lat"], "-", "");
		
		НоваяСтрока.СпасенныхЗаложников = СтрЗаменить(Человек["address"]["geo"]["lng"], "-", "");
		
	КонецЦикла;
	
КонецПроцедуры

// Проверяет является ли наемник исключаемым.
//
// Параметры:
//  Имя - Строка - Имя наемника.
//
// Возвращаемое значение:
//  Булево - Истина, если исключаемый наемник.
//
&НаСервереБезКонтекста
Функция ЭтоИсключаемыйНаемник(Имя)
	
	ИсключаемыеНаемники = Новый Массив;
	ИсключаемыеНаемники.Добавить("Ervin Howell");
	
	Возврат (НЕ ИсключаемыеНаемники.Найти(Имя) = Неопределено);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеССервера()
	
	URL = "https://jsonplaceholder.typicode.com/users";
	
	Попытка
		Ответ = КоннекторHTTP.Get(URL);
		Люди = КоннекторHTTP.КакJson(Ответ);
		
	Исключение

		Люди = Новый Массив;

		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("ЛучшиеНаемникиМесяца.Получение данных", УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
		
		ВызватьИсключение НСтр("ru = 'Не удалось получить данные с сервера.'");
		
	КонецПопытки;
	
	Возврат Люди;
	
КонецФункции

#КонецОбласти
